# == Class: consul
#
# Installs, configures, and manages consul
#
# === Parameters
#
# [*version*]
#   Specify version of consul binary to download.
#
# [*config_hash*]
#   Use this to populate the JSON config file for consul.
#
# [*pretty_config*]
#   Generates a human readable JSON config file. Defaults to `false`.
#
# [*pretty_config_indent*]
#   Toggle indentation for human readable JSON file. Defaults to `4`.
#
# [*install_method*]
#   Valid strings: `package` - install via system package
#                  `url`     - download and extract from a url
#                  `none`    - disable install
#   Defaults to `url`.
#
# [*core_package_name*]
#   Defaults to `consul`.
#
# [*core_package_ensure*]
#   Defaults to `installed`.
#
# [*core_package_checksum*]
#   See params.pp for defaults.
#
# [*ui_package_name*]
#   Defaults to `consul_ui`.
#
# [*ui_package_ensure*]
#   Defaults to `installed`.
#
# [*ui_package_checksum*]
#   See params.pp for defaults.
#
# [*url_digest*]
#   Digest type for package verification for url method. Supports md5, sha1, sha224, sha256, sha384, sha512.
#   Defaults to `sha256`.
#
# [*url_timeout*]
#   Timeout for fetching package for url method. Defaults to `60`.
#
# [*url_env_path*]
#   $PATH env var to use during url method operations. Defaults to `$::path`.
#    Note: when adding additional paths, append $::path. For example `url_env_path => "${::path}:/my/new/path"`
#
# [*url_tmp_dir*]
#   Temporary dir for url method operations. Defaults to `/tmp`.
#   Note: The directory must pre exist; `/var/tmp` is another option.
#
# [*restart_on_change*]
#   Determines whether to restart consul agent on $config_hash changes.
#   This will not affect reloads when service, check or watch configs change.
#   Defaults to `true`.
#
# [*extra_options*]
#   Extra arguments to be passed to the consul agent
#
# [*init_style*]
#   What style of init system your system uses.
#
# [*purge_config_dir*]
#   Purge config files no longer generated by Puppet
class consul (
  $version               = $consul::params::version,
  $manage_user           = true,
  $user                  = 'consul',
  $manage_group          = true,
  $extra_groups          = [],
  $purge_config_dir      = true,
  $group                 = 'consul',
  $join_wan              = false,
  $bin_dir               = '/usr/local/bin',
  $install_method        = $consul::params::install_method,
  $os                    = $consul::params::os,
  $arch                  = $consul::params::arch,
  $core_package_name     = $consul::params::core_package_name,
  $core_package_ensure   = $consul::params::core_package_ensure,
  $core_package_url      = undef,
  $core_package_url_base = $consul::params::core_package_url_base,
  $core_package_checksum = $consul::params::core_package_checksum,
  $ui_package_name       = $consul::params::ui_package_name,
  $ui_package_ensure     = $consul::params::ui_package_ensure,
  $ui_package_url        = undef,
  $ui_package_url_base   = $consul::params::ui_package_url_base,
  $ui_package_checksum   = $consul::params::ui_package_checksum,
  $url_digest            = 'sha256',
  $url_timeout           = 60,
  $url_env_path          = $::path,
  $url_tmp_dir           = '/tmp',
  $config_dir            = '/etc/consul',
  $extra_options         = '',
  $config_hash           = {},
  $config_defaults       = {},
  $pretty_config         = false,
  $pretty_config_indent  = 4,
  $service_enable        = true,
  $service_ensure        = 'running',
  $manage_service        = true,
  $restart_on_change     = true,
  $init_style            = $consul::params::init_style,
  $services              = {},
  $watches               = {},
  $checks                = {},
  $acls                  = {},
) inherits consul::params {

  $real_core_package_url = pick($core_package_url, "${core_package_url_base}/${version}_${os}_${arch}.zip")
  $real_ui_package_url   = pick($ui_package_url, "${ui_package_url_base}/${version}_web_ui.zip")

  validate_bool($purge_config_dir)
  validate_bool($manage_user)
  validate_array($extra_groups)
  validate_bool($manage_service)
  validate_bool($restart_on_change)
  validate_hash($config_hash)
  validate_hash($config_defaults)
  validate_bool($pretty_config)
  validate_integer($pretty_config_indent)
  validate_hash($services)
  validate_hash($watches)
  validate_hash($checks)
  validate_hash($acls)

  $config_hash_real = deep_merge($config_defaults, $config_hash)
  validate_hash($config_hash_real)

  if $config_hash_real['data_dir'] {
    $data_dir = $config_hash_real['data_dir']
  } else {
    $data_dir = undef
  }

  if $config_hash_real['ui_dir'] {
    $ui_dir = $config_hash_real['ui_dir']
  } else {
    $ui_dir = undef
  }

  if ($ui_dir and ! $data_dir) {
    warning('data_dir must be set to install consul web ui')
  }

  if ($config_hash_real['ports'] and $config_hash_real['ports']['rpc']) {
    $rpc_port = $config_hash_real['ports']['rpc']
  } else {
    $rpc_port = 8400
  }

  if ($config_hash_real['addresses'] and $config_hash_real['addresses']['rpc']) {
    $rpc_addr = $config_hash_real['addresses']['rpc']
  } elsif ($config_hash_real['client_addr']) {
    $rpc_addr = $config_hash_real['client_addr']
  } else {
    $rpc_addr = $::ipaddress_lo
  }

  if $services {
    create_resources(consul::service, $services)
  }

  if $watches {
    create_resources(consul::watch, $watches)
  }

  if $checks {
    create_resources(consul::check, $checks)
  }

  if $acls {
    create_resources(consul_acl, $acls)
  }

  $notify_service = $restart_on_change ? {
    true    => Class['consul::run_service'],
    default => undef,
  }

  anchor {'consul_first': }
  ->
  class { 'consul::install': } ->
  class { 'consul::config':
    config_hash => $config_hash_real,
    purge       => $purge_config_dir,
    notify      => $notify_service,
  } ->
  class { 'consul::run_service': } ->
  class { 'consul::reload_service': } ->
  anchor {'consul_last': }
}
